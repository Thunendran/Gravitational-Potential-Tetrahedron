# =============================================================================
# Example 03 (BigFloat): Laplacian Visualization of the Gravitational Potential Field
# -----------------------------------------------------------------------------
# Description:
#     This Julia script evaluates and visualizes the numerical Laplacian (∇²V)
#     of the gravitational potential generated by a homogeneous tetrahedron
#     using high-precision BigFloat arithmetic.
#
#     The potential is computed via the singularity-free analytical formulation
#     implemented in the `GP_TetrahedronBigFloat_Optimized.jl` module.
#     The script verifies that the computed field satisfies Poisson’s equation:
#
#                     ∇²V = 0          (outside the tetrahedron)
#                     ∇²V = -4πGρ       (inside the tetrahedron)
#
#     This test is performed in the z = 1/2 plane for a unit tetrahedron.
#
#     The figure displays:
#         - The numerical Laplacian field.
#         - The intersection polygon of the tetrahedron with z = 0.5.
#         - Annotated regions indicating “inside” and “outside” behavior.
#
# Reference:
#     Periyandy, T., & Bevis, M. (2025).
#     “The Gravitational Potential Inside, On and Outside of a Homogeneous
#     Tetrahedron.” 
#
# Authors:
#     Thunendran Periyandy (corresponding author)
#     Michael Bevis
#
# Date: November 2025
# =============================================================================

# --- Load the high-precision module ---
include("GP_TetrahedronBigFloat_Optimized.jl")
using .GP_TetrahedronBigFloat_Optimized

# --- Dependencies ---
using StaticArrays
using PyPlot
using PyCall
using Statistics

# --- Set BigFloat precision ---
setprecision(256; base=10)

# --- Physical constants ---
G = BigFloat(1.0)
sigma = BigFloat(1.0)

# --- Define tetrahedron vertices (unit tetrahedron) ---
vA = @SVector BigFloat[0, 0, 0]
vB = @SVector BigFloat[0, 1, 0]
vC = @SVector BigFloat[1, 0, 0]
vD = @SVector BigFloat[0, 0, 1]

# --- Construct tetrahedron object ---
tet = GP_TetrahedronBigFloat_Optimized.TetrahedronBigFloat(vA, vB, vC, vD)

# --- Gravitational potential function φ(x, y, z) ---
function phi(x::BigFloat, y::BigFloat, z::BigFloat)
    P = @SVector BigFloat[x, y, z]
    return GP_TetrahedronBigFloat_Optimized.compute_potential(tet, P; G=G, sigma=sigma)
end

# --- Numerical Laplacian (central difference) ---
function laplacian(f, x::BigFloat, y::BigFloat, z::BigFloat; h=BigFloat(1e-20))
    return (
        (f(x+h, y, z) - 2*f(x, y, z) + f(x-h, y, z)) / h^2 +
        (f(x, y+h, z) - 2*f(x, y, z) + f(x, y-h, z)) / h^2 +
        (f(x, y, z+h) - 2*f(x, y, z) + f(x, y, z-h)) / h^2
    )
end

# --- Grid setup ---
x_range = range(BigFloat(-0.1), BigFloat(0.6), length=200)
y_range = range(BigFloat(-0.1), BigFloat(0.6), length=200)
z_fixed = BigFloat(0.5)

laplacian_grid = Array{Float64}(undef, length(y_range), length(x_range))

println("Computing Laplacian grid...")
for (i, y) in enumerate(y_range)
    for (j, x) in enumerate(x_range)
        laplacian_grid[i, j] = Float64(laplacian(phi, x, y, z_fixed))
    end
end

# --- Step size for extent ---
dx = Float64(step(x_range))
dy = Float64(step(y_range))

# --- Custom colormap definition ---
@pyimport matplotlib.colors as colors_mpl
N = 50
colors_list = [(0.1, 0.4, 1.0)]  # blue start
for i in 1:(N-2)
    ratio = (i - 1.0) / (N - 3.0)
    push!(colors_list, (ratio, 1.0, 1.0 - ratio))
end
push!(colors_list, (1.0, 0.0, 0.0))  # red end
cmap_discrete = colors_mpl.ListedColormap(colors_list)

# --- Color normalization ---
vmin = Float64(-4 * BigFloat(pi))
vmax = 0.0
bounds = collect(range(vmin, vmax, length=N+1))
norm = colors_mpl.BoundaryNorm(bounds, N)

# --- Plotting ---
fig, ax = subplots(figsize=(10, 7))

im = ax.imshow(
    laplacian_grid,
    extent=(
        Float64(first(x_range)) - dx/2,
        Float64(last(x_range)) + dx/2,
        Float64(first(y_range)) - dy/2,
        Float64(last(y_range)) + dy/2
    ),
    origin="lower",
    cmap=cmap_discrete,
    norm=norm,
    interpolation="nearest",
    aspect="equal"
)

cbar = fig.colorbar(im, ax=ax, boundaries=bounds, spacing="uniform")
cbar.set_ticks([-12, -10, -8, -6, -4, -2, 0])
cbar.set_ticklabels(["-12", "-10", "-8", "-6", "-4", "-2", "0"])
cbar.set_label(L"\nabla^2 V", fontsize=14)

# --- Intersection of tetrahedron with z=0.5 plane ---
function intersect_line_plane(p1::SVector{3,BigFloat}, p2::SVector{3,BigFloat}, z_plane::BigFloat)
    dz = p2[3] - p1[3]
    if dz == BigFloat(0)
        return nothing
    end
    t = (z_plane - p1[3]) / dz
    if t > BigFloat(0) && t < BigFloat(1)
        return p1 .+ t .* (p2 .- p1)
    else
        return nothing
    end
end

intersection_points = SVector{3,BigFloat}[]
for (p1, p2) in [(vA,vD), (vB,vD), (vC,vD)]
    p = intersect_line_plane(p1, p2, z_fixed)
    if p !== nothing
        push!(intersection_points, p)
    end
end

intersection_points_xy = [Float64[p[1], p[2]] for p in intersection_points]

# --- Draw tetrahedron intersection boundary ---
if length(intersection_points_xy) >= 3
    x_coords = [p[1] for p in intersection_points_xy]
    y_coords = [p[2] for p in intersection_points_xy]
    ax.plot([x_coords..., x_coords[1]], [y_coords..., y_coords[1]],
            color="white", linewidth=2, label="Tetrahedral Boundary")
end

# --- Labels and annotations ---
ax.set_title(L"Laplacian Test in the $z = \frac{1}{2}$ Plane of the Tetrahedron", fontsize=14)
ax.set_xlabel("x", fontsize=18)
ax.set_ylabel("y", fontsize=18)

legend = ax.legend(loc="lower right", frameon=false)
for text_obj in legend.get_texts()
    text_obj.set_color("white")
end

if length(intersection_points_xy) >= 3
    interior_x = Float64(mean([p[1] for p in intersection_points_xy]))
    interior_y = Float64(mean([p[2] for p in intersection_points_xy]))
    ax.text(interior_x, interior_y, L"\nabla^2 V = -4\pi", color="white",
            fontsize=15, ha="center", va="center", fontweight="bold")
end

outside_x = Float64(last(x_range) - BigFloat(0.3))
outside_y = Float64(last(y_range) - BigFloat(0.3))
ax.text(outside_x, outside_y, L"\nabla^2 V = 0", color="white",
        fontsize=20, ha="center", va="center", fontweight="bold")

# --- Save and show ---
savefig("laplacian_unit_tetrahedron.png", dpi=300, bbox_inches="tight")
show()
